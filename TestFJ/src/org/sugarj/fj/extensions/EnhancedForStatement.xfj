package org.sugarj.fj.extensions;

public extension EnhancedForStatement {
	lexical syntax
	  "for" -> Keyword

	context-free syntax
	  "for" "(" Type ID ":" Expr ")" Statement -> Statement

    inductive definitions
      S-EnhancedFor:
      	typevars(CT) = X*
      	norm(CT, X*, T) = t
      	normPrim(CT, X*, T) = t
      	norm(CT, X*, FJIterator<T>) = org.sugarj.fj.FJIterator t::[]
      	CT; Ef; El |- e : s
      	CT.org.sugarj.fj.FJIterable = {[], iterator:[]->org.sugarj.fj.FJIterator A::[] :: [], Object, A::[]}
        CT.org.sugarj.fj.FJIterator = {[], hasNext:[]->boolean :: next:[]->A :: [], Object, A::[]}
        s =/=AType org.sugarj.fj.FJIterable t::[]
      	CT |- s <: org.sugarj.fj.FJIterable t::[]
      	it notin dom(El)
        x notin dom(El,it:org.sugarj.fj.FJIterator t::[])
      	CT; Ef; El,it:org.sugarj.fj.FJIterator t::[],x:t |- stm ~ void
      	CT; Ef; El |- stm* ~ rt
      	-----------------------------------------
      	CT; Ef; El |- for(T x : e) stm stm* ~ rt

	desugarings
	  { for(T x : e) stm stm* ~~~>
	    {
	  		FJIterator<T> it = e.iterator();
	  		while(it.hasNext()) {
	  	  		T x = it.next();
	  	  		stm
	  		}	
	  	}
	  	stm*
	  }
}
